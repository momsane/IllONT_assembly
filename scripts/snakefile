#command to symlink scratch: ln -s /scratch/mgarci14 ../../results/scratch
#wd=/work/FAC/FBM/DMF/pengel/general_data/syncom_ONT_seq/workflow/scripts
#workflow = scripts, envs, config
#data
#results
#logs
#benchmarks

config_file = "../config/metadata.tsv"
# Contains:
# - one column for sample,
# - one column with 1/0 to report if all the data is available,
# - one column with the type of adapter to trim
# - one column indicating if the samples passes the quality controls

# Import packages
import numpy as np
import pandas as pd

#Read config file
metadata = pd.read_table(config_file, sep='\t', header=0).set_index('sample', drop = False)

#List samples
SAMPLES = metadata.loc[metadata['avail'] == 1, 'sample'].tolist()
#SAMPLES = metadata['sample'].tolist()

# Put here the paths to the raw reads folders on the cluster
ILLUMINA_RAW = "../../data/raw_Illumina_reads"
ONT_RAW = "../../data/raw_ONT_reads"

rule all:
    input:
        # Illumina reads pre-processing
        "../../results/Illumina/check_integrity/integrity_files.txt",
        "../../results/Illumina/count_reads_bt/count_reads_bt.txt",
        "../../results/Illumina/fastqc_pretrim/all",
        "../../results/Illumina/fastqc_posttrim/all",
        # ONT reads pre-processing
        # "../../results/ONT/check_integrity/integrity_files.txt"
        expand("../../results/ONT/readstats_{stage}/readstats_{stage}_all.txt", stage = ["bf", "af"]),
        # ONT assembly
        "../../results/ONT/assembly/all_contigs_stats.txt",
        expand("../../results/ONT/assembly/bandage/{sample}_ONT_assembly_graph.jpg", sample = SAMPLES),
        # ONT polishing
        expand("../../results/ONT/polishing/{sample}_polishing_ONT", sample = SAMPLES),
        # Illumina polishing
        expand("../../results/final_polishing/{sample}_polishing_illumina", sample = SAMPLES)

# The pipeline assumes the reads from different lanes are already
# concatenated into a single file for both Illumina and ONT reads

############################ ILLUMINA ############################

# File names should be something like: {sample}_R1.fastq.gz

# First we QC the Illumina reads

## Check that all raw read files are complete
# Not needed if you could verify the md5sums

rule gzip_test_Illumina:
    input:
        R1 = ILLUMINA_RAW + "/{sample}_R1.fastq.gz",
        R2 = ILLUMINA_RAW + "/{sample}_R2.fastq.gz",
    output:
        "../../results/Illumina/check_integrity/samples/{sample}.txt"
    threads: 1
    resources:
        account = "pengel_general_data",
        runtime = "20m",
        mem_mb = 500
    shell:
        """
        if gzip -t {input.R1}; then
            echo {input.R1}": OK" > {output}
        else 
            echo {input.R1}": Inappropriate file type or format" > {output}
        fi
        if gzip -t {input.R2}; then
            echo {input.R2}": OK" >> {output}
        else 
            echo {input.R2}": Inappropriate file type or format" >> {output}
        fi
        """

# Incomplete/empty files are now printed to one file.
# Check the output of the following rule

rule check_integrity:
    input:
        expand("../../results/{platform}/check_integrity/samples/{sample}.txt", sample = SAMPLES, platform = "{platform}")
    output:
        "../../results/{platform}/check_integrity/integrity_files.txt"
    threads: 1
    resources:
        account = "pengel_general_data",
        runtime = "10m",
        mem_mb = 100
    shell:
        """
        echo {input} | xargs cat >> {output}
        """

# Count number of reads in each fastq file before trimming.

rule count_reads_bt:
    input:
        R1 = ILLUMINA_RAW + "/{sample}_R1.fastq.gz",
        R2 = ILLUMINA_RAW + "/{sample}_R2.fastq.gz",
    output:
        "../../results/Illumina/count_reads_bt/samples/count_reads_bt_{sample}.txt"
    params:
        tmp = "../../results/Illumina/count_reads_bt/samples/count_reads_bt_{sample}.temp.txt"
    conda:
        "../envs/seqkit-2.6.1.yaml"
    log:
        "../../logs/Illumina/count_reads_bt/samples/{sample}.log"
    threads: 1
    resources:
        account = "pengel_general_data",
        runtime = "1h",
        mem_mb = 2000
    shell:
        """ 
        seqkit stats -T -b -e {input.R1} > {params.tmp};
        seqkit stats -T -b -e {input.R2} >> {params.tmp};
        cat {params.tmp} | awk -v sample={wildcards.sample} 'NR == 2 {{print sample"\tR1\t"$0}}' > {output};
        cat {params.tmp} | awk -v sample={wildcards.sample} 'NR == 2 {{print sample"\tR2\t"$0}}' >> {output};
        rm {params.tmp}
        """

# Combining all the samples stats into one file 

rule combine_readcounts_bt:
    input:
        expand("../../results/Illumina/count_reads_bt/samples/count_reads_bt_{sample}.txt", sample = SAMPLES)
    output:
        "../../results/Illumina/count_reads_bt/count_reads_bt.txt"
    log:
        "../../logs/Illumina/count_reads_bt/combine_readcounts.log"
    threads: 1
    resources:
        account = "pengel_general_data",
        runtime = "30m",
        mem_mb = 1000
    shell:
        """
        echo -e "sample\tread\tfile\tformat\ttype\tnum_seqs\tsum_len\tmin_len\tavg_len\tmax_len" > {output};
        echo {input} | xargs cat >> {output}
        """

rule fastqc_bt:
    input:
        R1 = ILLUMINA_RAW + "/{sample}_R1.fastq.gz",
        R2 = ILLUMINA_RAW + "/{sample}_R2.fastq.gz",
    output:
        directory("../../results/Illumina/fastqc_pretrim/samples/{sample}")
    conda:
        "../envs/fastqc-0.11.8.yaml"
    log:
        "../../logs/Illumina/fastqc_pretrim/samples/{sample}.log"
    threads: 1
    resources:
        account = "pengel_general_data",
        runtime = "1h",
        mem_mb = 1000
    shell:
        """
        mkdir -p {output};
        fastqc -o {output} {input.R1} {input.R2}
        """

# Compile and visualize all fastqc reports together

rule multiqc_fastqc_bt:
    input:
        expand("../../results/Illumina/fastqc_pretrim/samples/{sample}", sample = SAMPLES)
    output:
        directory("../../results/Illumina/fastqc_pretrim/all")
    conda:
        "../envs/multiqc-1.6.yaml"
    params:
        ignore = ".zip"
    log:
        "../../logs/Illumina/fastqc_pretrim/multiqc.log"
    threads: 1
    resources:
        account = "pengel_general_data",
        runtime = "30m",
        mem_mb = 2000
    shell:
        "multiqc --interactive -f --ignore {params.ignore} -o {output} {input}"

# The metadata file is updated to indicate the adapter content and read length
# Then you need to upload the correct adapter sequence(s) to the data folder

### Trimming

def determine_adapters(wildcards):
    adapt=metadata.loc[metadata['sample'] == wildcards.sample, 'Illumina_adapter'].item()
    if (adapt=='NT'):
        return '../../data/adapters/Nextera_Transposase.fa'
    elif (adapt=='IU'):
        return '../../data/adapters/Illumina_Universal.fa'
    elif (adapt=='TS'):
        return '../../data/adapters/TruSeq.fa'

def determine_k(wildcards):
    adapt=metadata.loc[metadata['sample'] == wildcards.sample, 'Illumina_adapter'].item()
    if (adapt=='NT'):
        return 31 #maximum allowed by bbduk
    elif (adapt=='IU'):
        return 19
    elif (adapt=='TS'):
        return 31 

# hdist is set to 2 to allow for 2 mismatches in the adapter sequence
# which more stringent than the default of 1

rule bbduk_adapt:
    input:
        R1 = ILLUMINA_RAW + "/{sample}_R1.fastq.gz",
        R2 = ILLUMINA_RAW + "/{sample}_R2.fastq.gz",
    output:
        trim1 = "../../results/Illumina/trimmed_reads/{sample}_R1.trim.fastq.gz",
        trim2 = "../../results/Illumina/trimmed_reads/{sample}_R2.trim.fastq.gz"
    params:
        adapt = determine_adapters,
        k = determine_k,
        mink = 11,
        hdist = 2,
        hdist2 = 0,
        overlap = 12,
        minlen = 40,
        q = 26
    log:
        "../../logs/Illumina/read_trimming/{sample}_trimming"
    threads: 2
    conda:
        "../envs/bbmap-39.01.yaml"
    resources:
        account = "pengel_general_data",
        runtime = "1h",
        mem_mb = 2000
    shell:
        """
        bbduk.sh in={input.R1} in2={input.R2} out={output.trim1} out2={output.trim2} \
        ref={params.adapt} ktrim=r k={params.k} mink={params.mink} \
        hdist={params.hdist} hdist2={params.hdist2} \
        tpe=t tbo=t minoverlap={params.overlap} minlen={params.minlen} rcomp=f \
        qtrim=rl trimq={params.q}
        """

# Run fastqc after trimming

rule fastqc_pt:
    input:
        R1 = "../../results/Illumina/trimmed_reads/{sample}_R1.trim.fastq.gz",
        R2 = "../../results/Illumina/trimmed_reads/{sample}_R2.trim.fastq.gz"
    output:
        directory("../../results/Illumina/fastqc_posttrim/samples/{sample}")
    conda:
        "../envs/fastqc-0.11.8.yaml"
    log:
        "../../logs/Illumina/fastqc_posttrim/samples/{sample}.log"
    threads: 1
    resources:
        account = "pengel_general_data",
        runtime = "1h",
        mem_mb = 1000
    shell:
        """
        mkdir -p {output};
        fastqc -o {output} {input.R1} {input.R2}
        """

# Compile and visualize all fastqc reports together

rule multiqc_fastqc_pt:
    input:
        expand("../../results/Illumina/fastqc_posttrim/samples/{sample}", sample = SAMPLES)
    output:
        directory("../../results/Illumina/fastqc_posttrim/all")
    conda:
        "../envs/multiqc-1.6.yaml"
    params:
        ignore = ".zip"
    log:
        "../../logs/Illumina/fastqc_posttrim/multiqc.log"
    threads: 1
    resources:
        account = "pengel_general_data",
        runtime = "30m",
        mem_mb = 2000
    shell:
        "multiqc --interactive -f --ignore {params.ignore} -o {output} {input}"

############################ ONT ############################

# File names should be something like: {sample}.fastq.gz

# Check that files are intact
# Not needed if you could verify the md5sums

rule gzip_test_ONT:
    input:
        ONT_RAW + "/{sample}.fastq.gz"
    output:
        "../../results/ONT/check_integrity/samples/{sample}.txt"
    threads: 1
    resources:
        account = "pengel_general_data",
        runtime = "20m",
        mem_mb = 500
    shell:
        """
        if gzip -t {input}; then
            echo {input}": OK" > {output}
        else 
            echo {input}": Inappropriate file type or format" > {output}
        fi
        """

# Reuse rule check_integrity

### QC and trimming/filtering

# First we want to compute a few stats on the raw reads

rule readstats_bf:
    input:
        ONT_RAW + "/{sample}.fastq.gz"
    output:
        "../../results/ONT/readstats_bf/samples/readstats_bf_{sample}.txt"
    conda:
        "../envs/nanoq-0.10.0.yaml"
    log:
        "../../logs/ONT/readstats_bf/{sample}.log"
    threads: 1
    resources:
        account = "pengel_general_data",
        runtime = "1h",
        mem_mb = 2000
    shell:
        """ 
        nanoq -i {input} --json -s -vv > {output}
        """

# Combining all the read stats into one file: this rule parses all json outputs to turn
# them into a tabular format then combines them into one file

rule combine_readstats:
    input:
        expand("../../results/ONT/readstats_{stage}/samples/readstats_{stage}_{sample}.txt", sample = SAMPLES, stage = "{stage}")
    output:
        "../../results/ONT/readstats_{stage}/readstats_{stage}_all.txt"
    log:
        "../../logs/ONT/readstats_{stage}/all.log"
    threads: 1
    resources:
        account = "pengel_general_data",
        runtime = "30m",
        mem_mb = 1000
    run:
        import json
        import pandas as pd
        
        combined_df = pd.DataFrame()
        
        for input_file in input:
            sample_name = input_file.split('_')[-1].replace('.txt', '')
            
            with open(input_file, 'r') as f:
                data = json.load(f)
                data['sample'] = sample_name  # Add sample to the data
                df = pd.DataFrame.from_dict(pd.json_normalize(data), orient='columns')
                
                combined_df = pd.concat([combined_df, df], ignore_index=True)
        
        combined_df.set_index('sample', inplace=True)
        combined_df.to_csv(output[0], sep='\t', index=True)

# Then we filter the reads to remove short and low quality reads

rule read_filtering:
    input:
        ONT_RAW + "/{sample}.fastq.gz"
    output:
        "../../results/ONT/filtered_reads/{sample}_ONT_filtered.fastq.gz"
    params:
        min_readlen = 800,
        min_meanq = 10,
        length_weight = 10,
        target_bases = 400000000
    conda:
        "../envs/filtlong-0.2.1.yaml"
    log:
        "../../logs/ONT/filtlong/{sample}_filtlong.log"
    benchmark:
        "../../benchmarks/filtlong/{sample}_filtlong.benchmark"
    threads: 2
    resources:
        account = "pengel_general_data",
        runtime = "30m",
        mem_mb = 2000
    shell:
        """
        (filtlong --min_length {params.min_readlen} \
        --min_mean_q {params.min_meanq} \
        --length_weight {params.length_weight} \
        --target_bases {params.target_bases}  \
        {input} | gzip > {output})2> {log}
        """

# Recompute read stats after filtering

rule readstats_af:
    input:
        "../../results/ONT/filtered_reads/{sample}_ONT_filtered.fastq.gz"
    output:
        "../../results/ONT/readstats_af/samples/readstats_af_{sample}.txt"
    conda:
        "../envs/nanoq-0.10.0.yaml"
    log:
        "../../logs/ONT/readstats_af/{sample}.log"
    threads: 1
    resources:
        account = "pengel_general_data",
        runtime = "1h",
        mem_mb = 2000
    shell:
        """ 
        nanoq -i {input} --json -s -vv > {output}
        """

# Combine all the read stats into one file: reuse rule combine_readstats

### Assembly

rule ONT_assembly:
    input:
        "../../results/ONT/filtered_reads/{sample}_ONT_filtered.fastq.gz"
    output:
        directory("../../results/ONT/assembly/{sample}_assembly")
    log:
        "../../logs/ONT/assembly/{sample}_ONT_assembly.log"
    benchmark:
        "../../benchmarks/assembly/{sample}_ONT_assembly.benchmark"
    conda:
        "../envs/flye-2.9.5.yaml"
    threads: 4
    resources:
        account = "pengel_general_data",
        mem_mb = 12000,
        runtime= "1h"
    shell:
        "flye --threads {threads} --iterations 5 --nano-raw {input} --out-dir {output}"

# Next we compute some assembly stats

rule ONT_assembly_stats:
    input:
        fld = expand("../../results/ONT/assembly/{sample}_assembly", sample = SAMPLES),
        stats = expand("../../results/ONT/assembly/{sample}_assembly/30-contigger/contigs_stats.txt", sample = SAMPLES)
    output:
        "../../results/ONT/assembly/all_contigs_stats.txt"
    log:
        "../../logs/ONT/assembly/summay_assembly.log"
    params:
        tmp = "../../results/ONT/assembly/tmp.txt"
    threads: 1
    resources:
        account = "pengel_general_data",
        mem_mb = 500,
        runtime= "10m"
    shell:
        """
        echo -e 'file\tseq_name\tlength\tcoverage\tcircular\trepeat\tmult\ttelomere\talt_group\tgraph_path' > {params.tmp};
        for file in {input.stats}; do
        awk -v fname="$file" -F '\t' 'NR > 1 {{print fname"\t"$0}}' "$file" >> {params.tmp};
        done;
        sed '/#/ d' < {params.tmp} > {output};
        rm {params.tmp}
        """

# And visualize the assembly graph

rule bandage_assembly:
    input:
        fld = "../../results/ONT/assembly/{sample}_assembly",
        graph = "../../results/ONT/assembly/{sample}_assembly/assembly_graph.gfa"
    output:
        "../../results/ONT/assembly/bandage/{sample}_ONT_assembly_graph.jpg"
    threads: 2
    log:
        "../../logs/ONT/assembly/{sample}_ONT_assembly_graph.log"
    conda:
        "../envs/bandage-0.8.1.yaml"
    resources:
        account = "pengel_general_data",
        mem_mb = 2000,
        runtime= "1h"
    shell:
        "Bandage image {input.graph} {output}"

# Polish ONT assembly with ONT reads
# Do not forget to make script executable: chmod +x ONT_polishing.sh

# !!! Important note: use graphmap version 0.5.2, not later
# Newest versions (graphmap2) are meant only for RNA mapping

rule ONT_polishing:
    input:
        ONT_reads = "../../results/ONT/filtered_reads/{sample}_ONT_filtered.fastq.gz",
        ONT_asmbl = "../../results/ONT/assembly/{sample}_assembly/assembly.fasta"
    output:
        fld = directory("../../results/ONT/polishing/{sample}_polishing_ONT"),
        asmbl1 = "../../results/ONT/polishing/{sample}_polishing_ONT/01_first_Racon_graphmap/assembly/{sample}_first_Racon_Assembly.fasta",
        asmbl2 = "../../results/ONT/polishing/{sample}_polishing_ONT/02_second_Racon_graphmap/assembly/{sample}_second_Racon_Assembly.fasta"
    benchmark:
        "../../benchmarks/polishing/{sample}_ONT_polishing.benchmark"
    log:
        "../../logs/ONT/polishing/{sample}_ONT_polishing.log"
    conda:
        "../envs/ONT_polishing.yaml"
    threads: 4
    resources:
        account = "pengel_general_data",
        mem_mb = 2500,
        runtime= "1h"
    shell:
        "./ONT_polishing.sh {wildcards.sample} {input.ONT_reads} {input.ONT_asmbl} {output.fld}"

############################ Both ############################

# Polish ONT assembly with Illumina reads
# Do not forget to make script executable: chmod +x illumina_polishing.sh

rule illumina_polishing:
    input:
        ill_R1 = ILLUMINA_RAW + "/{sample}_R1.fastq.gz",
        ill_R2 = ILLUMINA_RAW + "/{sample}_R2.fastq.gz",
        fld = "../../results/ONT/polishing/{sample}_polishing_ONT",
        ONT_asmbl = "../../results/ONT/polishing/{sample}_polishing_ONT/02_second_Racon_graphmap/assembly/{sample}_second_Racon_Assembly.fasta"
    output:
        fld = directory("../../results/final_polishing/{sample}_polishing_illumina"),
        file1 = "../../results/final_polishing/{sample}_polishing_illumina/03_first_pilon_bowtie2/assembly/{sample}_first_Pilon_Assembly.fasta",
        file2 = "../../results/final_polishing/{sample}_polishing_illumina/04_second_pilon_bowtie2/assembly/{sample}_second_Pilon_Assembly.fasta",
        file3 = "../../results/final_polishing/{sample}_polishing_illumina/05_third_pilon_bowtie2/assembly/{sample}_third_Pilon_Assembly.fasta"
    benchmark:
        "../../benchmarks/final_polishing/{sample}_illumina_polishing.benchmark"
    log:
        "../../logs/final_polishing/{sample}_illumina_polishing.log"
    conda:
        "../envs/illumina_polishing.yaml"
    threads: 12
    resources:
        account = "pengel_general_data",
        mem_mb = 8000,
        runtime= "1h"
    shell:
        """
        (./illumina_polishing.sh {threads} {input.ONT_asmbl} \
        {input.ill_R1} {input.ill_R2} {output.fld} {wildcards.sample}) 2> {log}
        """

# rule assembly_QC:
#     input:
#         pilon3="../data/ONT_assembly/polishing/illumina/{sample}_polishing_illumina/05_third_pilon_bowtie2/assembly/{sample}_third_Pilon_Assembly.fasta",
#         ONT="../data/ONT_assembly/{sample}_ONT_assembly/assembly.fasta",
#         racon1="../data/ONT_assembly/polishing/ONT/{sample}_polishing_ONT/01_first_Racon_graphmap/assembly/{sample}_first_Racon_Assembly.fasta",
#         racon2="../data/ONT_assembly/polishing/ONT/{sample}_polishing_ONT/02_second_Racon_graphmap/assembly/{sample}_second_Racon_Assembly.fasta",
#         pilon1="../data/ONT_assembly/polishing/illumina/{sample}_polishing_illumina/03_first_pilon_bowtie2/assembly/{sample}_first_Pilon_Assembly.fasta",
#         pilon2="../data/ONT_assembly/polishing/illumina/{sample}_polishing_illumina/04_second_pilon_bowtie2/assembly/{sample}_second_Pilon_Assembly.fasta"
#     output:
#         directory("../results/ONT_assembly/polishing_QC/{sample}_assembly_QC/")
#     threads: 12
#     log:
#         "logs/ONT_assembly/polishing/quast/{sample}_assemblyQC.log"
#     conda:
#         "envs/quast.yaml"
#     resources:
#         account = "pengel_beemicrophage",
#         mem_mb = 16000,
#         runtime= "03:00:00"
#     shell:
#         "(quast.py \
#         -l 'FlyeAssembly,first_ONT_racon,second_ONT_racon,first_Illumina_pilon,second_Illumina_pilon' \
#         -R {input.pilon3} \
#           {input.ONT} \
#           {input.racon1} \
#           {input.racon2} \
#           {input.pilon1} \
#           {input.pilon2} \
#           -o  {output}) 2> {log}"

# rule ONT_assembly_mapping:
#         input:
#             dir="../data/ONT_assembly/polishing/illumina/{sample}_polishing_illumina",
#             bam="../data/ONT_assembly/polishing/illumina/{sample}_polishing_illumina/04_second_pilon_bowtie2/bowtie2/second_pilon_ONT2FinalAssembly_sorted.bam"
#         output:
#             map_reads="../results/ONT_assembly/polishing_QC/{sample}_assembly_QC/{sample}_mapping.txt",
#             map_bases="../results/ONT_assembly/bases_cov/{sample}_read_mapping_per_base.txt"
#         threads: 12
#         log:
#             "logs/ONT_assembly/polishing/mapping/{sample}_mapping.log"
#         conda:
#             "envs/illumina_polishing.yaml"
#         resources:
#             account = "pengel_beemicrophage",
#             mem_mb = 16000,
#             runtime= "03:00:00"
#         shell:
#             "(samtools flagstat {input.bam} > {output.map_reads}) 2> {log}; "
#             "(samtools depth -a {input.bam} > {output.map_bases}) 2> {log}"

# rule Parse_ONT_assembly_mapping:
#     input:
#         dir=expand("../data/ONT_assembly/polishing/illumina/{sample}_polishing_illumina",sample=config["samples"]),
#         map_reads=expand("../results/ONT_assembly/polishing_QC/{sample}_assembly_QC/{sample}_mapping.txt", sample=config["samples"]),
#         map_bases=expand("../results/ONT_assembly/bases_cov/{sample}_read_mapping_per_base.txt", sample=config["samples"])
#     output:
#         all_reads="../results/ONT_assembly/polishing_QC/all_mapstats.txt",
#         all_bases="../results/ONT_assembly/bases_cov/all_bases_cov.txt"
#     log:
#         "logs/ONT_assembly/polishing/mapping/summary_mapping.log"
#     conda:
#         "envs/base_R_env.yaml"
#     resources:
#         account = "pengel_beemicrophage",
#         mem_mb = 6000,
#         runtime= "00:30:00"
#     script:
#         "scripts/post_assembly/parse_ONTassembly.R"

# ################################################################################

# ############################ ANNOTATION ##########################################
# rule move_final_assemblies:
#     input:
#         asmbl=expand("../data/ONT_assembly/polishing/illumina/{sample}_polishing_illumina/05_third_pilon_bowtie2/assembly/{sample}_third_Pilon_Assembly.fasta", sample=config["samples"])
#     output:
#         db=directory("../resources/checkm_db"),
#         dir=directory("../results/ONT_assembly/final_assemblies/")
#     log:
#         "logs/annot_QC/move_assemblies/move_assembly.log"
#     resources:
#         account = "pengel_beemicrophage",
#         mem_mb = 6000,
#         runtime= "00:30:00"
#     shell:
#         "mkdir -p {output.dir}; "
#         "cp {input.asmbl} {output.dir}/"

# rule checkm_QC:
#     input:
#         dir="../results/ONT_assembly/final_assemblies/",
#     output:
#         dir=directory("../results/ONT_assembly/checkm_QC/"),
#         file="../results/ONT_assembly/checkm_QC/checkm_QC_stats.txt"
#     log:
#         "logs/annot_QC/checkm/checkm_QC.log"
#     threads: 16
#     conda:
#         "envs/checkm_env.yaml"
#     resources:
#         account = "pengel_beemicrophage",
#         mem_mb = 100000,
#         runtime= "01:00:00"
#     shell:
#         # "checkm_db={output.db}; "
#         # "echo ${{checkm_db}} | checkm data setRoot ${{checkm_db}}"
#         "export CHECKM_DATA_PATH=../resources/checkm_db"
#         "checkm lineage_wf {input.dir} {output.dir} -x .fasta -t {threads}; "
#         "checkm qa {output.dir}/lineage.ms {output.dir} -o 2 -f {output.file} --tab_table"

# rule checkm_plots:
#     input:
#         dir_asmbl="../results/ONT_assembly/final_assemblies/",
#         dir_checkm="../results/ONT_assembly/checkm_QC/"
#     output:
#         dir=directory("../results/ONT_assembly/checkm_QC/checkm_plots/")
#     log:
#         "logs/annot_QC/checkm/plot_checkm.log"
#     threads: 8
#     conda:
#         "envs/checkm_env.yaml"
#     resources:
#         account = "pengel_beemicrophage",
#         mem_mb = 5000,
#         runtime= "01:00:00"
#     shell:
#         "checkm marker_plot {input.dir_checkm} {input.dir_asmbl} {output.dir} -x fasta --image_type svg; "
#         "checkm coding_plot {input.dir_checkm} {input.dir_asmbl} {output.dir} 0 -x fasta --image_type svg; "
#         "checkm gc_plot {input.dir_asmbl} {output.dir} 0 -x fasta --image_type svg"

# rule DRAM_annot:
#     input:
#         asmbl="../results/ONT_assembly/final_assemblies/{sample}_third_Pilon_Assembly.fasta",
#         checkm="../results/ONT_assembly/checkm_QC/checkm_QC_stats.txt",
#         config="../resources/dram_config/DRAM_config.txt"
#     output:
#         dir=directory("../results/genomes_anntations/{sample}_annot_genome")
#     log:
#         "logs/annot_QC/dram/{sample}_annotation.log"
#     threads: 10
#     # conda:
#     #     "envs/dram.yaml" TODO does not work fails at pfam step --> using cluster one
#     resources:
#         account = "pengel_beemicrophage",
#         mem_mb = 100000,
#         runtime= "01:00:00"
#     shell:
#         #"(DRAM-setup.py import_config --config_loc {input.config}) 2> {log}; " #useful only if using conda dram_env
#         "source /etc/profile.d/lmodstacks.sh; " #import modules for DRAM
#         "export PATH=/dcsrsoft/spack/external/dram/v1.2.4/bin:$PATH; "
#         "dcsrsoft use old; "
#         "module load gcc/9.3.0 python; "
#         "module load hmmer mmseqs2 prodigal infernal trnascan-se barrnap; "
#         "(DRAM.py annotate -i {input.asmbl} \
#          -o {output.dir} --min_contig_size 999 \
#          --threads {threads} --verbose \
#          --checkm_quality {input.checkm}) 2> {log}" #annotate genomes with DRAM

# rule DRAM_distill:
#     input:
#         dir="../results/genomes_anntations/{sample}_annot_genome"
#     output:
#         dir=directory("../results/genomes_anntations/{sample}_distill")
#     log:
#         "logs/annot_QC/dram/{sample}_distill.log"
#     threads: 10
#     # conda:
#     #     "envs/dram.yaml" TODO does not work fails at pfam step --> using cluster one
#     resources:
#         account = "pengel_beemicrophage",
#         mem_mb = 10000,
#         runtime= "01:00:00"
#     shell:
#         #"(DRAM-setup.py import_config --config_loc {input.config}) 2> {log}; " #useful only if using conda dram_env
#         "source /etc/profile.d/lmodstacks.sh; " #import modules for DRAM
#         "export PATH=/dcsrsoft/spack/external/dram/v1.2.4/bin:$PATH; "
#         "dcsrsoft use old; "
#         "module load gcc/9.3.0 python; "
#         "module load hmmer mmseqs2 prodigal infernal trnascan-se barrnap; "
#         "(DRAM.py distill -i {input.dir}/annotations.tsv -o {output} --trna_path {input.dir}/trnas.tsv --rrna_path {input.dir}/rrnas.tsv) 2> {log}"

# ###################################DEFENSE SYSTEMS ##########################################################################
# rule DF_launch:
#     input:
#         faa="../results/genomes_anntations/{sample}_annot_genome/genes.faa"
#     output:
#         directory("../results/AVD_output/DF_output/{sample}_DF_output")
#     conda:
#         "envs/DF_env.yaml"
#     threads: 10
#     log:
#         "logs/defense_systems/DF/{sample}_DF.log"
#     resources:
#         account = "pengel_beemicrophage",
#         mem_mb = 10000,
#         runtime= "00:30:00"
#     shell:
#         #"defense-finder update; " # run only the first time you launch this ule, Idk how to make it more elegant
#         "(defense-finder run {input.faa} --out-dir {output}) 2> {log}"
