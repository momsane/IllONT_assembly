#wd=/work/FAC/FBM/DMF/pengel/general_data/syncom_ONT_seq/workflow/scripts
#command to symlink scratch: ln -s /scratch/mgarci14 ../../results/scratch
#workflow = scripts, envs, config
#data
#results
#logs
#benchmarks
configfile: "../config/config.yaml"
metadata_file = "../config/metadata.tsv"
# Contains:
# - one column for sample,
# - one column with 1/0 to report if all the data is available,
# - one column with the type of adapter to trim
# - one column indicating if the samples passes the quality controls

# Import packages
import numpy as np
import pandas as pd

#Read config file
metadata = pd.read_table(metadata_file, sep='\t', header=0).set_index('sample', drop = False)

#List samples to include for the read pre-processing and assembly
SAMPLES_ASS = metadata.loc[metadata['Illumina_avail'] == 1, 'sample'].tolist()
#List samples for which a genome was assembled
SAMPLES_GEN = metadata.loc[metadata['genome_assembled'] == 1, 'sample'].tolist()
#List samples to include for the downstream genome analysis
SAMPLES_ANNOT = metadata.loc[metadata['include_downstream'] == 1, 'sample'].tolist()

rule all:
    input:
        # First chunk: Illumina reads QC - add column "Illumina_adapter" to metadata at the end
        "../../results/Illumina/check_integrity/integrity_files.txt",
        "../../results/Illumina/count_reads_bt/count_reads_bt.txt",
        "../../results/Illumina/fastqc_pretrim/all",
        # Second chunk: Reads pre-processing and assembly - add column "genome_assembled" to metadata at the end
        "../../results/Illumina/fastqc_posttrim/all",
        "../../results/ONT/check_integrity/integrity_files.txt",
        expand("../../results/ONT/readstats_{stage}/readstats_{stage}_all.txt", stage = ["bf", "af"]),
        "../../results/hybracter/test",
        # expand("../../results/hybracter/{sample}_hybracter", sample = SAMPLES_ASS),
        expand("../../results/hybracter/{sample}_hybracter", sample = SAMPLES_GEN),
        # Third chunk: assemblies QC - add column "include_downstream" to metadata at the end
        "../../results/hybracter/hybracter_summary_all.tsv",
        "../../results/assembly/samples_assembly_stats.txt",
        expand("../../results/assembly/bandage/{sample}_assembly_graph.jpg", sample = SAMPLES_GEN),
        expand("../../results/final_assemblies/{sample}.fna", sample = SAMPLES_GEN),
        expand("../../results/final_assemblies_simple/{sample}.fna", sample = SAMPLES_GEN),
        "../../results/checkm",
        # Fourth chunk: polishing QC & genome analysis
        "../../results/Illumina_mapping/all_read_mapstats.txt",
        "../../results/checkm_plots",
        "../../results/gtdbtk_classify",
        expand("../../results/annotation/DRAM/{sample}_DRAM", sample = SAMPLES_ANNOT),
        expand("../../results/annotation/rRNAS/{sample}_16S.fna", sample = SAMPLES_ANNOT),
        expand("../../results/annotation/RGI/{sample}/{sample}_RGI_filtered.txt", sample = SAMPLES_ANNOT),
        expand("../../results/annotation/macsyfinder/{sample}", sample = SAMPLES_ANNOT)
        ## Test ##
        # "../../results/annotation/defense-finder/ESL0309"
        

# The pipeline assumes the reads from different lanes are already
# concatenated into a single file 

# File names should be something like: {sample}_R1.fastq.gz for Illumina and {sample}.fastq.gz for ONT

############################ Reads processing ############################

# First we QC the Illumina reads

# Check that all raw read files are complete

rule gzip_test_Illumina:
    input:
        R1 = config["Illumina_raw"] + "/{sample}_R1.fastq.gz",
        R2 = config["Illumina_raw"] + "/{sample}_R2.fastq.gz",
    output:
        "../../results/Illumina/check_integrity/samples/{sample}.txt"
    log:
        "../../logs/Illumina/check_integrity/{sample}.log"
    threads: 1
    resources:
        account = "pengel_general_data",
        runtime = "20m",
        mem_mb = 500
    shell:
        """
        if gzip -t {input.R1}; then
            echo {input.R1}": OK" > {output}
        else 
            echo {input.R1}": Inappropriate file type or format" > {output}
        fi
        if gzip -t {input.R2}; then
            echo {input.R2}": OK" >> {output}
        else 
            echo {input.R2}": Inappropriate file type or format" >> {output}
        fi
        """

# Incomplete/empty files are now printed to one file.
# Check the output of the following rule

rule check_integrity:
    input:
        expand("../../results/{platform}/check_integrity/samples/{sample}.txt", sample = SAMPLES_ASS, platform = "{platform}")
    output:
        "../../results/{platform}/check_integrity/integrity_files.txt"
    log:
        "../../logs/{platform}/check_integrity/all.log"
    threads: 1
    resources:
        account = "pengel_general_data",
        runtime = "10m",
        mem_mb = 100
    shell:
        """
        echo {input} | xargs cat >> {output}
        """

# Count number of reads in each fastq file before trimming.

rule count_reads_bt:
    input:
        R1 = config["Illumina_raw"] + "/{sample}_R1.fastq.gz",
        R2 = config["Illumina_raw"] + "/{sample}_R2.fastq.gz",
    output:
        tmp = temp("../../results/Illumina/count_reads_bt/samples/count_reads_bt_{sample}.temp.txt"),
        final = "../../results/Illumina/count_reads_bt/samples/count_reads_bt_{sample}.txt"
    conda:
        "../envs/seqkit-2.6.1.yaml"
    log:
        "../../logs/Illumina/count_reads_bt/samples/{sample}.log"
    threads: 1
    resources:
        account = "pengel_general_data",
        runtime = "1h",
        mem_mb = 2000
    shell:
        """ 
        seqkit stats -T -b -e {input.R1} > {output.tmp};
        seqkit stats -T -b -e {input.R2} >> {output.tmp};
        cat {output.tmp} | awk -v sample={wildcards.sample} 'NR == 2 {{print sample"\tR1\t"$0}}' > {output.final};
        cat {output.tmp} | awk -v sample={wildcards.sample} 'NR == 2 {{print sample"\tR2\t"$0}}' >> {output.final}
        """

# Combining all the samples stats into one file 

rule combine_readcounts_bt:
    input:
        expand("../../results/Illumina/count_reads_bt/samples/count_reads_bt_{sample}.txt", sample = SAMPLES_ASS)
    output:
        "../../results/Illumina/count_reads_bt/count_reads_bt.txt"
    log:
        "../../logs/Illumina/count_reads_bt/combine_readcounts.log"
    threads: 1
    resources:
        account = "pengel_general_data",
        runtime = "30m",
        mem_mb = 1000
    shell:
        """
        echo -e "sample\tread\tfile\tformat\ttype\tnum_seqs\tsum_len\tmin_len\tavg_len\tmax_len" > {output};
        echo {input} | xargs cat >> {output}
        """

rule fastqc_bt:
    input:
        R1 = config["Illumina_raw"] + "/{sample}_R1.fastq.gz",
        R2 = config["Illumina_raw"] + "/{sample}_R2.fastq.gz",
    output:
        directory("../../results/Illumina/fastqc_pretrim/samples/{sample}")
    conda:
        "../envs/fastqc-0.11.8.yaml"
    log:
        "../../logs/Illumina/fastqc_pretrim/samples/{sample}.log"
    threads: 1
    resources:
        account = "pengel_general_data",
        runtime = "1h",
        mem_mb = 1000
    shell:
        """
        mkdir -p {output};
        fastqc -o {output} {input.R1} {input.R2}
        """

# Compile and visualize all fastqc reports together

rule multiqc_fastqc_bt:
    input:
        expand("../../results/Illumina/fastqc_pretrim/samples/{sample}", sample = SAMPLES_ASS)
    output:
        directory("../../results/Illumina/fastqc_pretrim/all")
    conda:
        "../envs/multiqc-1.6.yaml"
    params:
        ignore = ".zip"
    log:
        "../../logs/Illumina/fastqc_pretrim/multiqc.log"
    threads: 1
    resources:
        account = "pengel_general_data",
        runtime = "30m",
        mem_mb = 2000
    shell:
        "multiqc --interactive -f --ignore {params.ignore} -o {output} {input}"

# The metadata file is updated to indicate the adapter content and read length
# Then you need to upload the correct adapter sequence(s) to the data folder

### Trimming

def determine_adapters(wildcards):
    adapt=metadata.loc[metadata['sample'] == wildcards.sample, 'Illumina_adapter'].item()
    if (adapt=='NT'):
        return '../../data/adapters/Nextera_Transposase.fa'
    elif (adapt=='IU'):
        return '../../data/adapters/Illumina_Universal.fa'
    elif (adapt=='TS'):
        return '../../data/adapters/TruSeq.fa'

def determine_k(wildcards):
    adapt=metadata.loc[metadata['sample'] == wildcards.sample, 'Illumina_adapter'].item()
    if (adapt=='NT'):
        return 31 #maximum allowed by bbduk
    elif (adapt=='IU'):
        return 19
    elif (adapt=='TS'):
        return 31 

# hdist is set to 2 to allow for 2 mismatches in the adapter sequence
# which is more stringent but safer than the default of 1

rule bbduk_adapt:
    input:
        R1 = config["Illumina_raw"] + "/{sample}_R1.fastq.gz",
        R2 = config["Illumina_raw"] + "/{sample}_R2.fastq.gz",
    output:
        trim1 = "../../results/Illumina/trimmed_reads/{sample}_R1.trim.fastq.gz",
        trim2 = "../../results/Illumina/trimmed_reads/{sample}_R2.trim.fastq.gz"
    params:
        adapt = determine_adapters,
        k = determine_k,
        mink = 11,
        hdist = 2,
        hdist2 = 0,
        overlap = 12,
        minlen = 40,
        q = 26
    log:
        "../../logs/Illumina/read_trimming/{sample}_trimming"
    threads: 2
    conda:
        "../envs/bbmap-39.01.yaml"
    resources:
        account = "pengel_general_data",
        runtime = "1h",
        mem_mb = 2000
    shell:
        """
        bbduk.sh in={input.R1} in2={input.R2} out={output.trim1} out2={output.trim2} \
        ref={params.adapt} ktrim=r k={params.k} mink={params.mink} \
        hdist={params.hdist} hdist2={params.hdist2} \
        tpe=t tbo=t minoverlap={params.overlap} minlen={params.minlen} rcomp=f \
        qtrim=rl trimq={params.q}
        """

# Run fastqc after trimming

rule fastqc_pt:
    input:
        R1 = "../../results/Illumina/trimmed_reads/{sample}_R1.trim.fastq.gz",
        R2 = "../../results/Illumina/trimmed_reads/{sample}_R2.trim.fastq.gz"
    output:
        directory("../../results/Illumina/fastqc_posttrim/samples/{sample}")
    conda:
        "../envs/fastqc-0.11.8.yaml"
    log:
        "../../logs/Illumina/fastqc_posttrim/samples/{sample}.log"
    threads: 1
    resources:
        account = "pengel_general_data",
        runtime = "1h",
        mem_mb = 1000
    shell:
        """
        mkdir -p {output};
        fastqc -o {output} {input.R1} {input.R2}
        """

# Compile and visualize all fastqc reports together

rule multiqc_fastqc_pt:
    input:
        expand("../../results/Illumina/fastqc_posttrim/samples/{sample}", sample = SAMPLES_ASS)
    output:
        directory("../../results/Illumina/fastqc_posttrim/all")
    conda:
        "../envs/multiqc-1.6.yaml"
    params:
        ignore = ".zip"
    log:
        "../../logs/Illumina/fastqc_posttrim/multiqc.log"
    threads: 1
    resources:
        account = "pengel_general_data",
        runtime = "30m",
        mem_mb = 2000
    shell:
        "multiqc --interactive -f --ignore {params.ignore} -o {output} {input}"

        
############################ ONT reads processing ############################

# File names should be something like: {sample}.fastq.gz

# Check that files are intact
# Not needed if you could verify the md5sums

rule gzip_test_ONT:
    input:
        config["ONT_raw"] + "/{sample}.fastq.gz"
    output:
        "../../results/ONT/check_integrity/samples/{sample}.txt"
    threads: 1
    resources:
        account = "pengel_general_data",
        runtime = "20m",
        mem_mb = 500
    shell:
        """
        if gzip -t {input}; then
            echo {input}": OK" > {output}
        else 
            echo {input}": Inappropriate file type or format" > {output}
        fi
        """

# Reuse rule check_integrity

### QC and trimming/filtering

# First we want to compute a few stats on the raw reads

rule readstats_bf:
    input:
        config["ONT_raw"] + "/{sample}.fastq.gz"
    output:
        "../../results/ONT/readstats_bf/samples/readstats_bf_{sample}.txt"
    conda:
        "../envs/nanoq-0.10.0.yaml"
    log:
        "../../logs/ONT/readstats_bf/{sample}.log"
    threads: 1
    resources:
        account = "pengel_general_data",
        runtime = "1h",
        mem_mb = 2000
    shell:
        """ 
        nanoq -i {input} --json -s -vv > {output}
        """

# Combining all the read stats into one file: this rule parses all json outputs to turn
# them into a tabular format then combines them into one file

rule combine_readstats:
    input:
        expand("../../results/ONT/readstats_{stage}/samples/readstats_{stage}_{sample}.txt", sample = SAMPLES_ASS, stage = "{stage}")
    output:
        "../../results/ONT/readstats_{stage}/readstats_{stage}_all.txt"
    log:
        "../../logs/ONT/readstats_{stage}/all.log"
    threads: 1
    resources:
        account = "pengel_general_data",
        runtime = "30m",
        mem_mb = 1000
    run:
        import json
        import pandas as pd
        
        combined_df = pd.DataFrame()
        
        for input_file in input:
            sample_name = input_file.split('_')[-1].replace('.txt', '')
            
            with open(input_file, 'r') as f:
                data = json.load(f)
                data['sample'] = sample_name  # Add sample to the data
                df = pd.DataFrame.from_dict(pd.json_normalize(data), orient='columns')
                
                combined_df = pd.concat([combined_df, df], ignore_index=True)
        
        combined_df.set_index('sample', inplace=True)
        combined_df.to_csv(output[0], sep='\t', index=True)

# Then we filter the reads to remove short and low quality reads

rule read_filtering:
    input:
        config["ONT_raw"] + "/{sample}.fastq.gz"
    output:
        "../../results/ONT/filtered_reads/{sample}_ONT_filtered.fastq.gz"
    params:
        min_readlen = 800,
        min_meanq = 10,
        length_weight = 10,
        target_bases = 400000000
    conda:
        "../envs/filtlong-0.2.1.yaml"
    log:
        "../../logs/ONT/filtlong/{sample}_filtlong.log"
    benchmark:
        "../../benchmarks/filtlong/{sample}_filtlong.benchmark"
    threads: 2
    resources:
        account = "pengel_general_data",
        runtime = "30m",
        mem_mb = 2000
    shell:
        """
        (filtlong --min_length {params.min_readlen} \
        --min_mean_q {params.min_meanq} \
        --length_weight {params.length_weight} \
        --target_bases {params.target_bases}  \
        {input} | gzip > {output})2> {log}
        """

# Recompute read stats after filtering

rule readstats_af:
    input:
        "../../results/ONT/filtered_reads/{sample}_ONT_filtered.fastq.gz"
    output:
        "../../results/ONT/readstats_af/samples/readstats_af_{sample}.txt"
    conda:
        "../envs/nanoq-0.10.0.yaml"
    log:
        "../../logs/ONT/readstats_af/{sample}.log"
    threads: 1
    resources:
        account = "pengel_general_data",
        runtime = "1h",
        mem_mb = 2000
    shell:
        """ 
        nanoq -i {input} --json -s -vv > {output}
        """

# Combine all the read stats into one file: reuse rule combine_readstats

############################ Hybracter ############################

# Hybracter is a snakemake-based software that runs the flye assembly,
# polishing with long reads (medaka, two rounds)
# polishing with short reads (polypolish and pypolca, 1 round each)
# with intermediate quality controls and comparisons between each polishing step

rule init_hybracter:
    input:
        expand("../../results/ONT/filtered_reads/{sample}_ONT_filtered.fastq.gz", sample = SAMPLES_ASS)
    output:
        install = directory("../../results/hybracter/installation"),
        test = directory("../../results/hybracter/test")
    params:
        config = "../config/config_hybracter.yaml"
    log:
        "../../logs/hybracter/init.log"
    threads: 1
    conda:
        "../envs/hybracter-0.9.0.yaml"
    resources:
        account = "pengel_general_data",
        mem_mb = 8000,
        runtime= "30m"
    shell:
        """
        hybracter install --configfile {params.config} -o {output.install};
        hybracter test-hybrid --configfile {params.config} -o {output.test}
        """

def get_size(wildcards):
    size = metadata.loc[metadata['sample'] == wildcards.sample, 'estimated_genome_size'].item()
    return int(size)

# a configfile is used to tell snakemake (hybracter uses snakemake >=8) to use the slurm plugin

rule hybracter:
    input:
        install = "../../results/hybracter/installation",
        test = "../../results/hybracter/test",
        filtered_ont = "../../results/ONT/filtered_reads/{sample}_ONT_filtered.fastq.gz",
        R1 = "../../results/Illumina/trimmed_reads/{sample}_R1.trim.fastq.gz",
        R2 = "../../results/Illumina/trimmed_reads/{sample}_R2.trim.fastq.gz"
    output:
        fld = directory("../../results/hybracter/{sample}_hybracter"),
        summary = "../../results/hybracter/{sample}_hybracter/FINAL_OUTPUT/hybracter_summary.tsv",
        stats = "../../results/hybracter/{sample}_hybracter/supplementary_results/flye_individual_summaries/{sample}_assembly_info.txt",
        graph = "../../results/hybracter/{sample}_hybracter/processing/assemblies/{sample}/assembly_graph.gfa"
    params:
        size = get_size,
        config = "../config/config_hybracter.yaml"
    benchmark:
        "../../benchmarks/hybracter/{sample}_hybracter.benchmark"
    log:
        "../../logs/hybracter/{sample}_hybracter.log"
    conda:
        "../envs/hybracter-0.9.0.yaml"
    threads: 1
    resources:
        account = "pengel_general_data",
        mem_mb = 8000,
        runtime= "5h"
    shell:
        """
        hybracter hybrid-single --configfile {params.config} -l {input.filtered_ont} \
        -1 {input.R1} -2 {input.R2} \
        -s {wildcards.sample} -c {params.size} -o {output} \
        -t {threads} --skip_qc --logic best    
        """

############################ Genomes QC ############################

# If some samples are of poor quality, hybracter will exit without producing an output
# so we need to update the /config/metadata.tsv file to exclude them from downstream analysis

# First we combine the hybracter summary outputs

rule hybracter_summary:
    input:
        fld = expand("../../results/hybracter/{sample}_hybracter", sample = SAMPLES_GEN),
        summaries = expand("../../results/hybracter/{sample}_hybracter/FINAL_OUTPUT/hybracter_summary.tsv", sample = SAMPLES_GEN)
    output:
        "../../results/hybracter/hybracter_summary_all.tsv"
    log:
        "../../logs/hybracter/summary_all.log"
    threads: 1
    resources:
        account = "pengel_general_data",
        mem_mb = 500,
        runtime= "5m"
    shell:
        """
        echo -e 'Sample\tComplete\tTotal_assembly_length\tNumber_of_contigs\tMost_accurate_polishing_round\tLongest_contig_length\tLongest_contig_coverage\tNumber_circular_plasmids' > {output};
        echo {input.summaries} | xargs tail -q -n +2 >> {output}
        """

# Then we combine the assembly stats from flye

rule flye_assembly_stats:
    input:
        fld = expand("../../results/hybracter/{sample}_hybracter", sample = SAMPLES_GEN),
        stats = expand("../../results/hybracter/{sample}_hybracter/supplementary_results/flye_individual_summaries/{sample}_assembly_info.txt", sample = SAMPLES_GEN)
    output:
        "../../results/assembly/samples_assembly_stats.txt"
    log:
        "../../logs/assembly/assembly_stats.log"
    params:
        tmp = "../../results/assembly/tmp.txt"
    threads: 1
    resources:
        account = "pengel_general_data",
        mem_mb = 500,
        runtime= "10m"
    shell:
        """
        echo -e 'file\tseq_name\tlength\tcoverage\tcircular\trepeat\tmult\ttelomere\talt_group\tgraph_path' > {params.tmp};
        for file in {input.stats}; do
        awk -v fname="$file" -F '\t' 'NR > 1 {{print fname"\t"$0}}' "$file" >> {params.tmp};
        done;
        sed '/#/ d' < {params.tmp} > {output};
        rm {params.tmp}
        """

# And visualize the assembly graph

rule bandage_assembly:
    input:
        "../../results/hybracter/{sample}_hybracter/processing/assemblies/{sample}/assembly_graph.gfa"
    output:
        "../../results/assembly/bandage/{sample}_assembly_graph.jpg"
    log:
        "../../logs/assembly/{sample}_assembly_graph.log"
    conda:
        "../envs/bandage-0.8.1.yaml"
    threads: 1
    resources:
        account = "pengel_general_data",
        mem_mb = 2000,
        runtime= "2h"
    shell:
        "Bandage image {input} {output}"

# After this check the hybracter output and genome QC outputs and indicate in the metadata file
# whether the assembly seems complete or not

# Rename contigs to have sample name, replace all spaces and "=" by "_" (it is crucial to not have any "=" for DRAM)
# and put all complete assemblies in a single folder

def get_fasta(wildcards):
    if metadata.loc[metadata['sample'] == wildcards.sample, 'complete'].item() == 1:
        return "../../results/hybracter/" + wildcards.sample + "_hybracter/FINAL_OUTPUT/complete/" + wildcards.sample + "_final.fasta"
    else:
        return "../../results/hybracter/" + wildcards.sample + "_hybracter/FINAL_OUTPUT/incomplete/" + wildcards.sample + "_final.fasta"

rule final_assemblies:
    input:
        "../../results/hybracter/{sample}_hybracter"
    output:
        "../../results/final_assemblies/{sample}.fna"
    params:
        fld = "../../results/final_assemblies",
        fasta = get_fasta
    log:
        "../../logs/final_assemblies/{sample}.log"
    threads: 1
    resources:
        account = "pengel_general_data",
        mem_mb = 1000,
        runtime= "10m"
    shell:
        """
        mkdir -p {params.fld};
        sample={wildcards.sample};
        sed -e "s/>/>$sample /g" -e "s/=/_/g" -e "s/ /_/g" {params.fasta} > {output}
        """

# A second version of the assembly with simplified fasta headers and 
# without the sample name because DRAM will add it itself

rule final_assemblies_nosample:
    input:
        fld = "../../results/hybracter/{sample}_hybracter",
        awk_script = "rename_scaffolds.awk",
    output:
        tmp = temp("../../results/final_assemblies_simple/{sample}.tmp"),
        fna = "../../results/final_assemblies_simple/{sample}.fna"
    params:
        fld = "../../results/final_assemblies_simple",
        fasta = get_fasta
    log:
        "../../logs/final_assemblies_simple/{sample}.log"
    threads: 1
    resources:
        account = "pengel_general_data",
        mem_mb = 1000,
        runtime= "10m"
    shell:
        """
        mkdir -p {params.fld};
        sed -e "s/=/_/g" -e "s/ /_/g" {params.fasta} > {output.tmp};
        awk -f {input.awk_script} {output.tmp} | tr -s '\n' > {output.fna}
        """

# Next we run checkM to assess the quality of the assemblies

rule checkm:
    input:
        expand("../../results/final_assemblies/{sample}.fna", sample = SAMPLES_GEN)
    output:
        fld = directory("../../results/checkm"),
        rextended = "../../results/checkm/checkm_report_extended.txt"
    params:
        fld = "../../results/final_assemblies",
        extension = "fna",
        tmpdir = "../../results/scratch/checkm.tmp",
        tmprep = "../../results/checkm/checkm_report_simple.txt",
        lineage = "../../results/checkm/lineage.ms"
    log:
        "../../logs/checkm/checkm.log"
    conda:
        "../envs/checkm-1.2.2.yaml"
    threads: 2
    resources:
        account = "pengel_general_data",
        mem_mb = 100000,
        runtime = "1h"
    shell:
        """
        mkdir -p {params.tmpdir};
        checkm lineage_wf -f {params.tmprep} --tab_table -x {params.extension} \
        -t {threads} --tmpdir {params.tmpdir} {params.fld} {output.fld};
        checkm qa -o 2 -f {output.rextended} --tab_table --tmpdir {params.tmpdir} \
        -t {threads} {params.lineage} {output.fld};
        rm {params.tmprep};
        rm -rf {params.tmpdir}
        """

# Check the checkM output to see if the genomes seem complete
# From here only complete genomes will be further processed

############################ Short reads mapping coverage ############################

# Map Illumina reads to assembly (hybracter does not provide the bam file)

rule map_Illumina:
    input:
        R1 = "../../results/Illumina/trimmed_reads/{sample}_R1.trim.fastq.gz",
        R2 = "../../results/Illumina/trimmed_reads/{sample}_R2.trim.fastq.gz",
        assembly = "../../results/final_assemblies/{sample}.fna"
    output:
        bam = "../../results/Illumina_mapping/bams/{sample}_assembly_sorted.bam",
        reads = "../../results/Illumina_mapping/read_mapping/{sample}_read_mapping.txt",
        bases = "../../results/Illumina_mapping/per_base/{sample}_read_mapping_per_base.txt"
    log:
        "../../logs/Illumina_mapping/{sample}_bowtie2.log"
    benchmark:
        "../../benchmarks/Illumina_mapping/{sample}_bowtie2.benchmark"
    params:
        buildf = "../../results/Illumina_mapping/build/{sample}",
        build = "../../results/Illumina_mapping/build/{sample}/{sample}"
    conda:
        "../envs/Illumina_mapping.yaml"
    threads: 6
    resources:
        account = "pengel_general_data",
        mem_mb = 3000,
        runtime = "20m"
    shell:
        """
        mkdir -p {params.buildf};
        bowtie2-build --quiet {input.assembly} {params.build};
        bowtie2 -1 {input.R1} -2 {input.R2} -x {params.build} --threads {threads} \
        --local --very-sensitive-local | samtools sort -O BAM -o {output.bam};
        samtools index {output.bam};
        (samtools flagstat {output.bam} > {output.reads}) 2> {log};
        (samtools depth -a {output.bam} > {output.bases}) 2> {log}
        """

rule parse_ONT_assembly_mapping:
    input:
        reads = expand("../../results/Illumina_mapping/read_mapping/{sample}_read_mapping.txt", sample = SAMPLES_ANNOT),
        bases = expand("../../results/Illumina_mapping/per_base/{sample}_read_mapping_per_base.txt", sample = SAMPLES_ANNOT)
    output:
        all_reads = "../../results/Illumina_mapping/all_read_mapstats.txt",
        all_bases = "../../results/Illumina_mapping/all_bases_cov.txt"
    log:
        "../../logs/Illumina_mapping/summary_mapping.log"
    conda:
        "../envs/R-4.3.2.yaml"
    threads: 1
    resources:
        account = "pengel_general_data",
        mem_mb = 6000,
        runtime= "30m"
    script:
        "./parse_ONTassembly.R"

rule checkm_plots:
    input:
        assemblies = expand("../../results/final_assemblies/{sample}.fna", sample = SAMPLES_ANNOT),
        dir_checkm = "../../results/checkm"
    output:
        fld = directory("../../results/checkm_plots")
    params:
        dir_asmbl = "../../results/final_assemblies",
        extension = "fna"
    benchmark:
        "../../benchmarks/checkm_plots.benchmark"
    log:
        "../../logs/checkm/plot_checkm.log"
    threads: 1
    conda:
        "../envs/checkm-1.2.2.yaml"
    resources:
        account = "pengel_general_data",
        mem_mb = 2000,
        runtime= "20m"
    shell:
        """
        checkm marker_plot {input.dir_checkm} {params.dir_asmbl} {output.fld} -x {params.extension} --image_type svg;
        checkm coding_plot {input.dir_checkm} {params.dir_asmbl} {output.fld} 0 -x {params.extension} --image_type svg;
        checkm gc_plot {params.dir_asmbl} {output.fld} 0 -x {params.extension} --image_type svg;
        checkm nx_plot {params.dir_asmbl} {output.fld} -x {params.extension} --image_type svg
        """

############################ Classification & annotation ##########################################

rule gtdb_classify:
    input:
        expand("../../results/final_assemblies/{sample}.fna", sample = SAMPLES_ANNOT)
    output:
        fld = directory("../../results/gtdbtk_classify"),
        report = "../../results/gtdbtk_classify/classify/gtdbtk.bac120.summary.tsv"
    log:
        "../../logs/gtdbtk/gtdbtk_classify"
    benchmark:
        "../../benchmarks/gtdbtk_classify.benchmark"
    params:
        dir_asmbl = "../../results/final_assemblies",
        extension = "fna",
        mashdb = config["Mash_db"],
        tmpdir = "../../results/scratch/gtdbtk_classify/",
        scratchdir = "../../results/scratch/gtdbtk_classify_pplacer"
    conda:
        "../envs/gtdbtk-2.4.0.yaml"
    threads: 4
    resources:
        account = "pengel_general_data",
        mem_mb = 20000,
        runtime = "6h" # increase if you have a lot of genomes or genomes from poorly characterized clades
    shell:
        """
        mkdir -p {params.tmpdir};
        gtdbtk classify_wf --genome_dir {params.dir_asmbl} --mash_db {params.mashdb} --scratch_dir {params.scratchdir} \
        --out_dir {output.fld} --extension {params.extension} --cpus {threads} --tmpdir {params.tmpdir}
        """

rule DRAM:
    input:
        asmbl = "../../results/final_assemblies_simple/{sample}.fna",
        checkm = "../../results/checkm/checkm_report_extended.txt",
        gtdbtk = "../../results/gtdbtk_classify/classify/gtdbtk.bac120.summary.tsv",
        config = "../config/dram-1.4.6_config.txt"
    output:
        fld1 = directory("../../results/annotation/DRAM/{sample}_DRAM"),
        fld2 = directory("../../results/annotation/DRAM/{sample}_DRAM/distill"),
        annot = "../../results/annotation/DRAM/{sample}_DRAM/annotations.tsv",
        faa = "../../results/annotation/DRAM/{sample}_DRAM/genes.faa",
        fna = "../../results/annotation/DRAM/{sample}_DRAM/genes.fna",
        gbk = "../../results/annotation/DRAM/{sample}_DRAM/genbank/{sample}.gbk",
        scaff = "../../results/annotation/DRAM/{sample}_DRAM/scaffolds.fna",
        gff = "../../results/annotation/DRAM/{sample}_DRAM/genes.gff",
        met = "../../results/annotation/DRAM/{sample}_DRAM/distill/metabolism_summary.xlsx",
        prod1 = "../../results/annotation/DRAM/{sample}_DRAM/distill/product.html",
        prod2 = "../../results/annotation/DRAM/{sample}_DRAM/distill/product.tsv",
        stats = "../../results/annotation/DRAM/{sample}_DRAM/distill/genome_stats.tsv"
    params:
        trnas = "../../results/annotation/DRAM/{sample}_DRAM/trnas.tsv",
        rrnas = "../../results/annotation/DRAM/{sample}_DRAM/rrnas.tsv"
    log:
        "../../logs/annotation/DRAM/{sample}_dram.log"
    conda:
        "../envs/dram-1.4.6.yaml"
    threads: 8
    resources:
        account = "pengel_general_data",
        mem_mb = 30000,
        runtime= "6h"
    shell:
        """
        rm -rf {output.fld1};
        (DRAM-setup.py import_config --config_loc {input.config}) 2> {log};
        (DRAM.py annotate -i {input.asmbl} -o {output.fld1} --min_contig_size 999 \
        --checkm_quality {input.checkm} --gtdb_taxonomy {input.gtdbtk} --threads {threads} --verbose) 2> {log};
        if [[ -f {params.trnas} && -f {params.rrnas} ]]; then
            (DRAM.py distill -i {output.annot} -o {output.fld2} --trna_path {params.trnas} \
            --rrna_path {params.rrnas}) 2>> {log}
        elif [[ -f {params.trnas} && ! -f {params.rrnas} ]]; then
            (DRAM.py distill -i {output.annot} -o {output.fld2} --trna_path {params.trnas}) 2>> {log}
        elif [[ ! -f {params.trnas} && ! -f {params.rrnas} ]]; then
            (DRAM.py distill -i {output.annot} -o {output.fld2}) 2>> {log}
        fi
        """

rule extract_16s:
    input:
        "../../results/annotation/DRAM/{sample}_DRAM/scaffolds.fna"
    output:
        barr = "../../results/annotation/rRNAS/{sample}_rrnas.fna",
        seqs = "../../results/annotation/rRNAS/{sample}_16S.fna"
    params:
        fld = "../../results/annotation/rRNAS",
        king = "bac"
    log:
        "../../logs/annotation/barrnap/{sample}_barrnap.log"
    conda:
        "../envs/dram-1.4.6.yaml"
    threads: 1
    resources:
        account = "pengel_general_data",
        mem_mb = 2000,
        runtime= "1h"
    shell:
        """
        mkdir -p {params.fld};
        barrnap --threads {threads} --kingdom {params.king} --outseq {output.barr} < {input};
        sed -i "s/>/>{wildcards.sample}|/g" {output.barr};
        grep -A 1 "16S" {output.barr} > {output.seqs}
        """

# remove all * from input fasta before passing it to rgi

rule rgi:
    input:
        "../../results/annotation/DRAM/{sample}_DRAM/genes.faa"
    output:
        fld = directory("../../results/annotation/RGI/{sample}"),
        raw = "../../results/annotation/RGI/{sample}/{sample}_RGI_raw.txt",
        filtered = "../../results/annotation/RGI/{sample}/{sample}_RGI_filtered.txt"
    params:
        tmp = "../../results/annotation/RGI/{sample}/{sample}.faa.tmp",
        db = "/work/FAC/FBM/DMF/pengel/general_data/mgarci14/RGI_db/card.json",
        bn = "../../results/annotation/RGI/{sample}/{sample}_RGI_raw"
    log:
        "../../logs/annotation/RGI/{sample}_rgi.log"
    conda:
        "../envs/rgi-6.0.3.yaml"
    threads: 1
    resources:
        account = "pengel_general_data",
        mem_mb = 4000,
        runtime= "1h"
    shell:
        """
        rgi load --card_json {params.db} --local;
        sed 's/*//g' {input} > {params.tmp}
        rgi main --input_sequence {params.tmp} --output_file {params.bn} --local --clean --include_loose -t protein -n {threads};
        awk -F '\t' '($10>40) && ($21>70) {{print}}' {output.raw} > {output.filtered};
        rm -rf {params.tmp}
        """

# If macsyfinder is installed with the latest version it works
# the conda environment also contains biopython

rule macsydata:
    output:
        fld = directory("../../results/annotation/macsyfinder/data"),
        file = "../../results/annotation/macsyfinder/macsydata/macsydata.txt"
    log:
        "../../logs/annotation/macsyfinder/macsydata.log"
    conda:
        "../envs/macsyfinder-2.1.4.yaml"
    threads: 1
    resources:
        account = "pengel_general_data",
        mem_mb = 2000,
        runtime= "15m"
    shell:
        """
        mkdir -p {output.fld};
        mkdir -p ${{CONDA_PREFIX}}/share/macsyfinder/models/profiles;
        mkdir -p ${{CONDA_PREFIX}}/share/macsyfinder/models/definitions;
        mkdir -p ${{CONDA_PREFIX}}/share/macsyfinder/doc/profiles;
        mkdir -p ${{CONDA_PREFIX}}/share/macsyfinder/doc/definitions;
        macsydata install --target ${{CONDA_PREFIX}}/share/macsyfinder TXSScan;
        touch {output.file}
        """

# macsyfinder can run on both unordered and ordered genomes, but in the ordered mode more informative outputs are produced
# normally the genes.faa file from DRAM is already ordered
# if you start from a .faa file that did not come from DRAM (i.e. downloaded from NCBI), ensure the genes are correctly ordered

rule reorder_genes:
    input:
        data = "../../results/annotation/macsyfinder/macsydata/macsydata.txt",
        gff = "../../results/annotation/DRAM/{sample}_DRAM/genes.gff",
        faa = "../../results/annotation/DRAM/{sample}_DRAM/genes.faa"
    output:
        "../../results/annotation/macsyfinder/ordered_genomes/{sample}_ordered.faa"
    params:
        fld = "../../results/annotation/macsyfinder/ordered_genomes"
    conda:
        "../envs/macsyfinder-2.1.4.yaml"
    log:
        "../../logs/annotation/macsyfinder/order_genes/{sample}.log"
    threads: 1
    resources:
        account = "pengel_general_data",
        mem_mb = 2000,
        runtime= "1h"
    script:
        "order_genes.py"

rule macsyfinder:
    input:
        data = "../../results/annotation/macsyfinder/macsydata/macsydata.txt",
        faa = "../../results/annotation/macsyfinder/ordered_genomes/{sample}_ordered.faa"
    output:
        directory("../../results/annotation/macsyfinder/{sample}")
    log:
        "../../logs/annotation/macsyfinder/{sample}_df.log"
    conda:
        "../envs/macsyfinder-2.1.4.yaml"
    threads: 1
    resources:
        account = "pengel_general_data",
        mem_mb = 4000,
        runtime= "1h"
    shell:
        """
        macsyfinder --sequence-db {input.faa} -o {output} --models-dir ${{CONDA_PREFIX}}/share/macsyfinder --models TXSScan all --db-type ordered_replicon -w {threads} -vv
        """
        
## There are currently issues with macsyfinder so I cannot run defense-finder (it needs macsyfinder)

# rule defense_finder_update:
#     output:
#         fld = directory("../../results/annotation/defense-finder/update"),
#         file = "../../results/annotation/defense-finder/update/update.txt"
#     log:
#         "../../logs/annotation/defense-finder/update.log"
#     conda:
#         "../envs/defense-finder-1.3.0.yaml"
#     threads: 1
#     resources:
#         account = "pengel_general_data",
#         mem_mb = 4000,
#         runtime= "1h"
#     shell:
#         """
#         mkdir -p {output.fld};
#         defense-finder update;
#         touch {output.file}
#         """

# rule defense_finder:
#     input:
#         update = "../../results/annotation/defense-finder/update/update.txt",
#         faa = "../../results/annotation/DRAM/{sample}_DRAM/genes.faa"
#     output:
#         directory("../../results/annotation/defense-finder/{sample}")
#     log:
#         "../../logs/annotation/defense-finder/{sample}_df.log"
#     conda:
#         "../envs/defense-finder-1.3.0.yaml"
#     threads: 1
#     resources:
#         account = "pengel_general_data",
#         mem_mb = 4000,
#         runtime= "1h"
#     shell:
#         """
#         (defense-finder run {input.faa} --out-dir {output}) 2> {log}
#         """